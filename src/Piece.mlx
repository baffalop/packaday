type t = [
  | `Rect
  | `Thumb
  | `Corner
  | `Snake
  | `U
  | `L
  | `T
  | `Z
] [@@mel.string]

(** intermediate representation for individual squares of a piece *)
type segment =
  | O (* gap *)
  | H (* filled *)

type shape_spec = segment Array.t Array.t

let spec_of_t : t -> shape_spec = function
  | `Rect -> [|
      [| H; H |];
      [| H; H |];
      [| H; H |];
    |]
  | `L -> [|
      [| H; H |];
      [| H; O |];
      [| H; O |];
      [| H; O |];
    |]
  | `T -> [|
    [| H; O |];
    [| H; H |];
    [| H; O |];
    [| H; O |];
  |]
  | `U -> [|
    [| H; H |];
    [| H; O |];
    [| H; H |];
    |]
  | `Z -> [|
      [| H; H; O |];
      [| O; H; O |];
      [| O; H; H |];
    |]
  | `Corner -> [|
      [| H; H; H |];
      [| O; O; H |];
      [| O; O; H |];
    |]
  | `Snake -> [|
      [| O; H |];
      [| H; H |];
      [| H; O |];
      [| H; O |];
    |]
  | `Thumb -> [|
      [| O; H |];
      [| H; H |];
      [| H; H |];
    |]

module Shape = struct
  let cell_size = 8

  let[@react.component] make ~(t:t) () =
    let spec = spec_of_t t in
    let rows = Array.length spec in
    let cols = if rows > 0 then Array.length spec.(0) else 0 in
    let height = rows * cell_size in
    let width = cols * cell_size in
    <svg
      width=(string_of_int width)
      height=(string_of_int height)
      viewBox=(Printf.sprintf "0 0 %d %d" width height)

      className="piece-shape"
    >
      (spec |> Array.mapi (fun row_idx row ->
        row |> Array.mapi (fun col_idx seg ->
          match seg with
          | O -> React.null
          | H ->
            let x = col_idx * cell_size in
            let y = row_idx * cell_size in
            <rect
              key=(string_of_int row_idx ^ "-" ^ string_of_int col_idx)
              x=(string_of_int x)
              y=(string_of_int y)
              width=(string_of_int cell_size)
              height=(string_of_int cell_size)
              className="fill-rose-500"
            />
        ) |> React.array
      ) |> React.array)
    </svg>
end

module Button = struct
  let[@react.component] make ~(t:t) () =
    <button
      type_="button"
      className="piece-btn p-1 rounded cursor-pointer hover:bg-rose-900 max-w-min max-h-min"
      onClick=(fun _ -> Js.Console.log t)
    >
      <Shape t />
    </button>
end

module Panel = struct
  let[@react.component] make () =
    <div className="panel bg-rose-900/60 border border-rose-800 rounded-lg px-4 py-8 grid gap-4 grid-cols-2">
      ([| `Rect; `Thumb; `Corner; `Snake; `U; `L; `T; `Z |]
      |> Array.map (fun t -> <Button t />)
      |> React.array)
    </div>
end
