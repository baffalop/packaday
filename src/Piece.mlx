type t = [
  | `Rect
  | `Thumb
  | `Corner
  | `Snake
  | `U
  | `L
  | `T
  | `Z
]

module Shape = struct
  (** intermediate representation for individual squares of a piece *)
  type segment =
    | O (* gap *)
    | H (* filled *)

  type shape_spec = segment Array.t Array.t

  let spec_of_t : t -> shape_spec = function
    | `Rect -> [|
      [| H; H |];
      [| H; H |];
      [| H; H |];
    |]
    | `Thumb -> [|
      [| O; H |];
      [| H; H |];
      [| H; H |];
    |]
    | `Corner -> [|
      [| H; H; H |];
      [| O; O; H |];
      [| O; O; H |];
    |]
    | `L -> [|
      [| H; H |];
      [| H; O |];
      [| H; O |];
      [| H; O |];
    |]
    | `Snake -> [|
      [| H; O |];
      [| H; H |];
      [| O; H |];
      [| O; H |];
    |]
    | `T -> [|
      [| H; O |];
      [| H; H |];
      [| H; O |];
      [| H; O |];
    |]
    | `U -> [|
      [| H; H |];
      [| O; H |];
      [| H; H |];
      |]
    | `Z -> [|
      [| H; H; O |];
      [| O; H; O |];
      [| O; H; H |];
    |]

  let[@react.component] make ~(t:t) ?(cellSize=8) ?(className="fill-rose-500") () =
    let spec = spec_of_t t in
    let rows = Array.length spec in
    let cols = if rows > 0 then Array.length spec.(0) else 0 in
    let height = rows * cellSize in
    let width = cols * cellSize in
    <svg
      width=(string_of_int width)
      height=(string_of_int height)
      viewBox=(Printf.sprintf "0 0 %d %d" width height)

      className="piece-shape"
    >
      (spec |> Array.mapi (fun row_idx row ->
        row |> Array.mapi (fun col_idx seg ->
          match seg with
          | O -> React.null
          | H ->
            let x = col_idx * cellSize in
            let y = row_idx * cellSize in
            <rect
              key=(Printf.sprintf "%d-%d" row_idx col_idx)
              x=(string_of_int x)
              y=(string_of_int y)
              width=(string_of_int cellSize)
              height=(string_of_int cellSize)
              className
            />
        ) |> React.array
      ) |> React.array)
    </svg>
end

module Button = struct
  external stopPropagation : React.Event.Mouse.t -> unit = "stopPropagation" [@@mel.send]

  let[@react.component] make ~(t:t) ?onSelect () =
    <button
      type_="button"
      className="piece-btn p-1 rounded cursor-pointer hover:bg-rose-900 max-w-min max-h-min"
      onClick=(fun e ->
        stopPropagation e;
        Option.iter (fun f ->
          let coord : Geometry.coord = {
            x = React.Event.Mouse.clientX e;
            y = React.Event.Mouse.clientY e;
          } in
          f (t, coord)) onSelect)
    >
      <Shape t />
    </button>
end

module Panel = struct
  let[@react.component] make ?onPieceSelect () =
    <div className="panel bg-rose-900/60 border border-rose-800 rounded-lg px-4 py-8 grid gap-4 grid-cols-2">
      ([| `Rect; `Thumb; `L; `Snake; `T; `U; `Z; `Corner |]
      |> Array.map (fun t -> <Button t ?onSelect=onPieceSelect />)
      |> React.array)
    </div>
end

module Floating = struct
  let[@react.component] make ~(piece:t) ~(initial:Geometry.coord) () =
    let pos = Option.value (Hooks.useMouse ()) ~default:initial in
    let style = ReactDOM.Style.make
      ~left:(Printf.sprintf "%dpx" pos.x)
      ~top:(Printf.sprintf "%dpx" pos.y)
      ()
    in
    <div style className="floating-piece fixed -translate-x-1/2 -translate-y-1/2 pointer-events-none">
      <Shape t=piece cellSize=64 className="fill-rose-400/40" />
    </div>
end
