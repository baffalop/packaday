type t = Shape.piece

module Button = struct
  external stopPropagation : React.Event.Mouse.t -> unit = "stopPropagation" [@@mel.send]

  let[@react.component] make ~(t:t) ~onSelect () =
    <button
      type_="button"
      className="piece-btn p-1 rounded cursor-pointer hover:bg-rose-900 max-w-min max-h-min"
      onClick=(fun e ->
        stopPropagation e;
        let coord : Geometry.coord = {
          x = React.Event.Mouse.clientX e;
          y = React.Event.Mouse.clientY e;
        } in
        onSelect (t, coord))
    >
      <Shape t=(Shape.of_piece t) />
    </button>
end

module Floating = struct
  let[@react.component] make ~(piece:t) ~(initial:Geometry.coord) () =
    let pos = Option.value (Hooks.useMouse ()) ~default:initial in
    let style = ReactDOM.Style.make
      ~left:(Printf.sprintf "%dpx" pos.x)
      ~top:(Printf.sprintf "%dpx" pos.y)
      ()
    in
    <div style className="floating-piece fixed -translate-x-1/2 -translate-y-1/2 pointer-events-none">
      <Shape t=(Shape.of_piece piece) cellSize=66 className="fill-rose-400/40" />
    </div>
end
